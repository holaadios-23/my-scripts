//──────────── Stone Face ────────────
// Feito por Kauê
// Discord: Kaueyjo
// Versão 0.2 (beta)



//──────────── COORDINATES ────────────
//You can change the coordinates here =]
//Fx1 = X position | and Fy1 = Y position

var Fx1 = 1
var Fy1 = 1

var Fx2 = Fx1 + 1
var Fy2 = Fy1 + 1

var Fx3
var Fy3 = Fy1 + 2

var Fx4 = Fx1 + 7
var Fy4 = Fy1 + 3

?player.direction = 1
 Fx3 = Fx1 + 3
:
 Fx3 = Fx1 + 2



//──────────── COLORS ────────────
//You can change the colors here =］
//pColor = Panel Color | hColor = Head Color | eColor = Eyes Color

var pColor = #white
var hColor = #white
var eColor = #white
var mColor = #white



?item.right = mask
 ?item.right.ID = commom
  mColor = #cyan

 ?item.right.ID = rare
  mColor = #yellow

 ?item.right.ID = heroic
  mColor = #green

 ?item.right.ID = epic
  mColor = #blue

 ?item.right.ID = legendary
  mColor = #red

 ?item.right.ID = transcendent
  mColor = #rainbow



//──────────── PANEL ────────────

>`@Fx1@,@Fy1@,@pColor@,ascii
┌──────┐
│      │
│      │
│      │
└──────┘
asciiend

?bighead = false & time.month = 12 & time.day >= 18 & time.day <= 25
  >`@Fx1@,@Fy1@,#DC143C,ascii
(¯(\/)¯)
#´####`#
asciiend



?time.month = 10 & time.day >= 24 & time.day <= 31
 ?player.direction = 1
  >`@Fx1@,@Fy1@,#green,####/´
 :
  >`@Fx1@,@Fy1@,#green,###`\

?buffs.string = vampiric
 ?player.direction = 1
  >`@Fx1@,@Fy4@,#white,\¯#####/
 :
  >`@Fx1@,@Fy4@,#white,\#####¯/

?loc=uulaa
 ?time % 60 < 20
  >`@Fx4@,@Fy3@,♫
 ?time % 60 > 40
  >`@Fx4@,@Fy3@,♪



//──────────── HEAD ────────────

?bighead = false
 >`@Fx2@,@Fy2@,@hColor@,ascii
#´¯¯`#
|####|
#.__,#
asciiend

 ?time.month = 10 & time.day >= 24 & time.day <= 31
  >`@Fx2@,@Fy2@,#FF7518,ascii
#´¯¯`#
||##||
#.__,#
asciiend

 ?totaltime >= 18000
  ?player.direction = 1
   >`@Fx2@,@Fy2@,#white,ascii
#,──.#
( #,#)
 ¯`＂´ 
asciiend
  :
   >`@Fx2@,@Fy2@,#white,ascii
#,──.#
(#,# )
 `＂´¯ 
asciiend

 ?buffs.string = berserk
  ?player.direction = 1
   >`@Fx2@,@Fy2@,#red,ascii
###,
#
#´
asciiend
  :
   >`@Fx2@,@Fy2@,#red,##,

 ?item.right = mask
  ?player.direction = 1
   >`@Fx2@,@Fy2@,@mColor@,ascii
#//¯`#
((###｝
/`──´\
asciiend
  :
   >`@Fx2@,@Fy2@,@mColor@,ascii
#´¯\\#
｛###))
/`──´\
asciiend



?bighead = true
 >`@Fx1@,@Fy1@,@hColor@,ascii
#,¯¯¯¯.#
/      \
│ #### │
\      /
#`____´#
asciiend

 ?time.month = 10 & time.day >= 24 & time.day <= 31
  >`@Fx1@,@Fy1@,#FF7518,ascii
#,¯¯¯¯.#
/ /  \ \
│ │##│ │
\ \  / /
#`____´#
asciiend

 ?totaltime >= 18000
  ?player.direction = 1
   >`@Fx1@,@Fy1@,#white,ascii
#,¯¯¯¯.#
/      \
│ #### │
\   ^  /
#¯`'＂'´#
asciiend
  :
   >`@Fx1@,@Fy1@,#white,ascii
#,¯¯¯¯.#
/      \
│ #### │
\  ^   /
#`'＂'´¯#
asciiend

 ?time.month = 12 & time.day >= 18 & time.day <= 25
  ?player.direction = 1
   >`@Fx1@,@Fy1@,#white,O
   >`@Fx1@,@Fy4@,#FF4500,####═-
   >`@Fx1@,@Fy1@,#DC143C,ascii
#,¯¯¯¯.#
/ _──¯¯
│/
asciiend
  :
   >`@Fx1@,@Fy1@,#white,#######O
   >`@Fx1@,@Fy4@,#FF4500,##-═
   >`@Fx1@,@Fy1@,#DC143C,ascii
#,¯¯¯¯.#
#¯¯──_ \
######\|
asciiend

 ?buffs.string = berserk
  ?player.direction = 1
   >`@Fx2@,@Fy2@,#red,ascii
###/
#
#´
asciiend
  :
   >`@Fx2@,@Fy2@,#red,ascii
##/
#
´
asciiend

 ?item.right = mask
  ?player.direction = 1
   >`@Fx1@,@Fy1@,@mColor@,ascii
#/¯/¯¯`#
/ /    \
\ \### /
 \ `  / 
#`____´#
asciiend
  :
   >`@Fx1@,@Fy1@,@mColor@,ascii
#´¯¯\¯\#
/    \ \
\ ###/ /
 \  ´ / 
#`____´#
asciiend



//──────────── EYES ────────────

>`@Fx3@,@Fy3@,@eColor@,o#o

?foe=boss | buffs.string = berserk
 >`@Fx3@,@Fy3@,@eColor@,ò#ó

?foe=boss & foe.hp <= 0 | loc=uulaa
 >`@Fx3@,@Fy3@,@eColor@,^#^

?loc=intro & ai.paused
 >`@Fx3@,@Fy3@,@eColor@,`#`

?pickup.distance <= 5 | harvest = boulder & harvest.distance <= 3 
 >`@Fx3@,@Fy3@,@eColor@,,#,

?foe.state = 33 | foe.state = 133
 >`@Fx3@,@Fy3@,@eColor@,>#-

?foe.state = 142
 >`@Fx3@,@Fy3@,@eColor@,O#O

?foe.state = 143 | item.right = bardiche & ai.idle | item.right = heavy & ai.idle
 >`@Fx3@,@Fy3@,@eColor@,>#<

?buffs.string = vampiric
 >`@Fx3@,@Fy3@,@eColor@,`#´

?time % 300 = 0
 >`@Fx3@,@Fy3@,@eColor@,-#-



?time.month = 10 & time.day >= 24 & time.day <= 31
 >`@Fx3@,@Fy3@,#yellow,▲#▲

 ?foe=boss & foe.hp <= 0
  >`@Fx3@,@Fy3@,#yellow,^#^

 ?foe=boss | buffs.string = berserk
  >`@Fx3@,@Fy3@,#yellow,\#/

 ?foe.state = 142
  >`@Fx3@,@Fy3@,#yellow,Δ#Δ

 ?time % 300 = 0
  >`@Fx3@,@Fy3@,#yellow,-#-



//──────────────────────────────

?buffs.string = invisibility
 >`@Fx1@,@Fy1@,@pColor@,ascii
┌──────┐
│      │
│      │
│      │
└──────┘
asciiend

?totaltime <= 1800 & foe=boss & foe.hp <= 0 & time % 30 < 20
  >`@Fx1@,@Fy1@,#FFE169,#\`#####
  >`@Fx1@,@Fy2@,#FFC300,###¯¯###
  >`@Fx1@,@Fy1@,#926C15,#####´/#
  >`@Fx1@,@Fy1@,#FFD235,ascii
###/####
##´#####
asciiend
  >`@Fx1@,@Fy1@,#C9980B,ascii
####\###
#####`##
asciiend




// ~ KSRN
